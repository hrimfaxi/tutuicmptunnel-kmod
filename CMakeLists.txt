cmake_minimum_required(VERSION 3.16)
project(tutuicmptunnel-kmod VERSION 1.5 LANGUAGES C)

if(NOT CMAKE_BUILD_TYPE)
  message(FATAL_ERROR "CMAKE_BUILD_TYPE is not set. Please set it to continue.\n"
                      "Example: cmake -DCMAKE_BUILD_TYPE=Release ..\n"
                      "Valid types: Release, Debug, RelWithDebInfo, MinSizeRel")
endif()

set(CMAKE_C_STANDARD 11)
add_compile_options(-Wall -Wextra -Wno-error)
add_definitions(-D_FILE_OFFSET_BITS=64 -D_GNU_SOURCE=1 -DPROJECT_NAME=${CMAKE_PROJECT_NAME} -DINSTALL_PREFIX_DIR=${CMAKE_INSTALL_PREFIX})
include_directories(common)
include_directories(${CMAKE_CURRENT_BINARY_DIR})
add_compile_definitions(
  VERSION_MAJOR=${CMAKE_PROJECT_VERSION_MAJOR}
  VERSION_MINOR=${CMAKE_PROJECT_VERSION_MINOR}
  VERSION_STR=\"${CMAKE_PROJECT_VERSION}\"
)

# 默认使用libsodium而不是tucrypto
option(USE_TUCRYPTO "Enable builtin crypto instead of libsodium" OFF)
message(STATUS "Use tucrypto: ${USE_TUCRYPTO}")
option(USE_LLVM_FOR_KMOD "Enable llvm for kmodule building" OFF)
option(ENABLE_HARDEN_MODE "Enable security hardening compiler and linker flags" OFF)
if (ENABLE_HARDEN_MODE)
  message(STATUS "Secure mode enabled. Adding security hardening flags.")
  add_compile_definitions(_FORTIFY_SOURCE=2)
  set(HARDEN_C_FLAGS
    #-Werror
    -Werror=implicit
    -Werror=incompatible-pointer-types
    -Werror=int-conversion
    -fPIE
  )
  include(CheckCCompilerFlag)
  macro(check_and_add_c_flag FLAG)
    string(REGEX REPLACE "[^A-Za-z0-9_]" "_" VAR_SUFFIX ${FLAG})
    check_c_compiler_flag("${FLAG}" "C_HAS_${VAR_SUFFIX}")
    if(C_HAS_${VAR_SUFFIX})
      message(STATUS "  -> Compiler supports: ${FLAG}")
      list(APPEND HARDEN_C_FLAGS "${FLAG}")
    else()
      message(STATUS "  -> Compiler does NOT support: ${FLAG} (skipping)")
    endif()
  endmacro()

  check_and_add_c_flag("-fstack-clash-protection")
  check_and_add_c_flag("-fstack-protector-strong")
  check_and_add_c_flag("-Wtrampolines")
  check_and_add_c_flag("-Wbidi-chars=any")
  check_and_add_c_flag("-fno-delete-null-pointer-checks")
  check_and_add_c_flag("-fno-strict-overflow")
  check_and_add_c_flag("-fno-strict-aliasing")
  check_and_add_c_flag("-ftrivial-auto-var-init=zero")
  check_and_add_c_flag("-fstrict-flex-arrays=3")
  check_and_add_c_flag("-mbranch-protection=standard")
  check_and_add_c_flag("-fexceptions")
  check_and_add_c_flag("-fcf-protection=full")
  if (NOT C_HAS__fcf_protection_full)
    check_and_add_c_flag("-fcf-protection=branch")
  endif()

  if(UNIX AND NOT APPLE)
    set(HARDEN_LINKER_FLAGS
          -pie
          -Wl,-z,nodlopen
          -Wl,-z,noexecstack
          -Wl,-z,relro
          -Wl,-z,now
          -Wl,--as-needed
          -Wl,--no-copy-dt-needed-entries
          -Wl,-z,max-page-size=4096
          -Wl,--gc-sections
    )
  endif()
endif()

configure_file(
  "${PROJECT_SOURCE_DIR}/kmod/dkms.conf.in"
  "${PROJECT_SOURCE_DIR}/kmod/dkms.conf"
  @ONLY
)

configure_file(
  "${PROJECT_SOURCE_DIR}/kmod/Makefile.in"
  "${PROJECT_SOURCE_DIR}/kmod/Makefile"
  @ONLY
)

#set(CMAKE_VERBOSE_MAKEFILE ON)

if(USE_LLVM_FOR_KMOD)
  list(APPEND KMOD_MAKE_ARGS "LLVM=${LLVM}")
endif()

include(CheckCXXSourceRuns)
execute_process(
  COMMAND ${CMAKE_MAKE_PROGRAM} --no-silent -v
  RESULT_VARIABLE MAKE_NOSILENT_RESULT
  OUTPUT_QUIET ERROR_QUIET
)

if(MAKE_NOSILENT_RESULT EQUAL 0)
  set(MAKE_NOSILENT_ARG "--no-silent")
else()
  set(MAKE_NOSILENT_ARG "")
endif()

if (NOT WIN32)
  add_subdirectory(ktuctl)
endif()

add_subdirectory(common)

# 查找libsodium
find_path(SODIUM_INCLUDE_DIR sodium.h PATH_SUFFIXES sodium)
find_library(SODIUM_LIBRARY sodium)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
  SODIUM
  REQUIRED_VARS SODIUM_INCLUDE_DIR SODIUM_LIBRARY
  FAIL_MESSAGE "Could not find libsodium."
)

if (SODIUM_FOUND)
  if (USE_TUCRYPTO)
    message(WARNING "libsodium is found, but overridden by tucrypto (USE_TUCRYPTO=ON)")
    set(USE_TUCRYPTO 1)
    set(USE_SODIUM 0)
  else()
    message(STATUS "libsodium found and in use")
    set(USE_TUCRYPTO 0)
    set(USE_SODIUM 1)
  endif()
else()
  # 找不到 libsodium，切换为 tucrypto
  message(WARNING "libsodium not found; switching to tucrypto")
  set(USE_TUCRYPTO 1)
  set(USE_SODIUM 0)
endif()

if(USE_TUCRYPTO)
  include(CheckCSourceCompiles)

  # poly1305需要检查int128可用性
  # 检测 __int128 可用（GCC/Clang 通常支持，MSVC 不支持）
  set(CODE_HAS_INT128 "
  #include <stdint.h>
  int main(void) {
    __int128 a = 0;
    unsigned __int128 b = 0;
    (void)a; (void)b;
    return 0;
  }")
  check_c_source_compiles("${CODE_HAS_INT128}" HAVE___INT128)

  # 检测 MSVC 64-bit
  if (MSVC AND CMAKE_SIZEOF_VOID_P EQUAL 8)
    set(HAVE_MSVC_64BIT 1)
  else()
    set(HAVE_MSVC_64BIT 0)
  endif()

  # 检测 GCC/Clang 版本与 64-bit 目标（如需保持与你原逻辑一致）
  set(HAVE_GCC_4_4_64BIT 0)
  if (CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
    if (CMAKE_SIZEOF_VOID_P EQUAL 8)
      if (CMAKE_C_COMPILER_ID STREQUAL "GNU")
        if (CMAKE_C_COMPILER_VERSION VERSION_GREATER_EQUAL 4.4)
          set(HAVE_GCC_4_4_64BIT 1)
        endif()
      else()
        set(HAVE_GCC_4_4_64BIT 1)
      endif()
    endif()
  endif()

  if (HAVE___INT128 OR HAVE_MSVC_64BIT OR HAVE_GCC_4_4_64BIT)
    set(POLY1305_HAVE_FAST_64BIT 1)
  else()
    set(POLY1305_HAVE_FAST_64BIT 0)
  endif()

  include(CheckIncludeFile)
  check_include_file(sys/random.h HAVE_SYS_RANDOM_H)

  add_subdirectory(tucrypto)
endif()

# 生成配置头
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/tucrypto_config.h.in
  ${CMAKE_CURRENT_BINARY_DIR}/tucrypto_config.h
  @ONLY
)

add_subdirectory(tuserver)

if (WIN32)
  add_subdirectory(win32)
endif()

if(CMAKE_BUILD_TYPE STREQUAL "Release")
  set(CMAKE_INSTALL_DO_STRIP ON)
endif()

# vim: set sw=2 expandtab :
